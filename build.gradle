plugins {
    id 'java'
    id 'com.google.protobuf' version '0.9.4'
}

group = 'com.example'
version = '1.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21) // Usar JDK 21 explÃ­citamente
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'io.grpc:grpc-netty-shaded:1.57.0'
    implementation 'io.grpc:grpc-protobuf:1.57.0'
    implementation 'io.grpc:grpc-stub:1.57.0'
    implementation 'com.google.protobuf:protobuf-java:3.24.1'

    testImplementation 'junit:junit:4.13.2'

    implementation 'com.datastax.oss:java-driver-core:4.15.0'

}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.24.1"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.57.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                grpc {}
            }
        }
    }
}

compileJava {
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform()
}

tasks.register('runServer', JavaExec) {
    mainClass.set('com.example.PersonaServer')
    classpath = sourceSets.main.runtimeClasspath
}

tasks.register('runClient', JavaExec) {
    mainClass.set('com.example.PersonaClient')
    classpath = sourceSets.main.runtimeClasspath
}
